query = select_query | update_query | alter_query ;
select_query = { select_clause } , [ ampersand , where_clause , ampersand , order_clause , ampersand , range_clause ] | [ ampersand , group_by_clause , ampersand , range_clause ], { ampersand , message };
select_clause = "select=" , select_term , { comma , select_term } ;
select_term =  { function_call } , select_element ;
function_call = function , open_bracket , function_parameter , close_bracket ;
function_parameter = select_term | asterisk ;
select_element = key | array ;
key = key_characters , { dot , key_characters } ;
array = key , open_brace , array_slice , [ array_selection ] , close_brace ;
array_slice = index | asterisk ;
array_selection = pipe, key , { comma , key } ;
where_clause = "where=" , where_term , { comma , combinator , where_term } ;
where_term = {open_bracket} , where_element , equals , operator , equals , quote , where_value , quote , {close_bracket} ;
where_element = key | array - array_selection ;
where_value = ".+" | "\\'" ;
group_by_clause = "group_by=" , key , { comma , key } ;
order_clause = "order=" , select_term , dot , direction ;
direction = "desc" | "asc" ;
range_clause = "range=" , equals , index , dot , index ;
update_query = set_clause , where_clause , { ampersand , message } ;
set_clause = "set=" , key , { comma, key } ;
alter_query = alter_clause ;
alter_clause = "alter=" , 'name' , equals , alter_operation , dot , alter_value ;
alter_operation = "eq" ;
alter_value = key_characters ;
operator = ? eq, gt, gte, lt, lte, neq, like, ilike, not, is, in ? ;
combinator = and | or ;
and = "and:" ;
or = "or:" ;
index = ? 0-9+ ? ;
asterisk = "*" ;
pipe = "|" ;
open_brace = "[" ;
close_brace = "]" ;
open_bracket = "(" ;
close_bracket = ")" ;
function = ? count, avg, sum, min, max, min_ts, max_ts ? ;
key_characters = ? a-zA-Z_ ? ;
dot = "." ;
comma = "," ;
ampersand = "&" ;
equals = "=" ;
quote = "'" ;
message = "message=" , quote , ".+" , quote ;